_help:
  @just -l

mirrors := '''
  mirrors:
    "localhost:5000":
      endpoint:
        - http://k3d-ftl:5000
'''

# Create a k3d cluster for FTL including a local registry.
setup:
  k3d registry create ftl --port 5000
  k3d cluster create ftl --api-port 6550 -p "8892:80@loadbalancer" --agents 2 \
      --registry-use k3d-ftl:5000 \
      --registry-config '{{mirrors}}'

# Delete the FTL k3d cluster and registry.
teardown:
  k3d cluster delete ftl
  k3d registry delete ftl

deploy:
  kubectl kustomize --load-restrictor=LoadRestrictionsNone | kubectl apply -f -

delete:
  kubectl kustomize --load-restrictor=LoadRestrictionsNone | kubectl delete -f -

delete-db:
  kubectl delete pvc postgres-db-ftl-pg-cluster-1-0

events:
  kubectl get events -w

ps:
  kubectl get deployment,pod,statefulset,svc,configmap,pv,pvc,ingress -o wide

logs *args:
  kubectl logs -f {{args}}

exec pod *args:
  kubectl exec -t {{pod}} -- {{args}}

enter pod *args="bash":
  kubectl exec -it {{pod}} -- {{args}}

psql:
  just enter statefulset.apps/ftl-pg-cluster-1 env PGPASSWORD=secret psql -U postgres ftl

ftl-status:
  just exec deployment/ftl-controller ./ftl status
